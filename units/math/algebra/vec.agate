# SPDX-License-Identifier: MIT
# Copyright (c) 2022 Julien Bernard
#
# Vectors

class Vec2 {
  construct new(value) {
    @x = @y = value
  }

  construct new(x, y) {
    @x = x
    @y = y
  }

  x { @x }
  x=(val) { @x = val }
  y { @y }
  y=(val) { @y = val }

  [index] { Object.field(this, index) }
  [index]=(value) { Object.field(this, index, value) }

  size { 2 }

  + { this }
  - { Vec2.new(-@x, -@y) }

  +(other) { Vec2.new(@x + other.x, @y + other.y) }
  -(other) { Vec2.new(@x - other.x, @y - other.y) }
  *(other) { Vec2.new(@x * other.x, @y * other.y) }
  /(other) { Vec2.new(@x / other.x, @y / other.y) }
  ==(other) { @x == other.x && @y == other.y }
  !=(other) { @x != other.x || @y != other.y }

  static dot(lhs, rhs) { lhs.x * rhs.x + lhs.y * rhs.y }
}

class Vec3 {
  construct new(value) {
    @x = @y = @z = value
  }

  construct new(x, y, z) {
    @x = x
    @y = y
    @z = z
  }

  x { @x }
  x=(val) { @x = val }
  y { @y }
  y=(val) { @y = val }
  z { @z }
  z=(val) { @z = val }

  [index] { Object.field(this, index) }
  [index]=(value) { Object.field(this, index, value) }

  size { 3 }

  + { this }
  - { Vec3.new(-@x, -@y, -@z) }

  +(other) { Vec3.new(@x + other.x, @y + other.y, @z + other.z) }
  -(other) { Vec3.new(@x - other.x, @y - other.y, @z - other.z) }
  *(other) { Vec3.new(@x * other.x, @y * other.y, @z * other.z) }
  /(other) { Vec3.new(@x / other.x, @y / other.y, @z / other.z) }
  ==(other) { @x == other.x && @y == other.y && @z == other.z }
  !=(other) { @x != other.x || @y != other.y || @z != other.z }

  static dot(lhs, rhs) { lhs.x * rhs.x + lhs.y * rhs.y + lhs.z * rhs.z }
}

class Vec {
  construct new(size, value) {
    assert(size > 0, "Vec must have a positive size")
    @data = Array.new(size, value)
  }

  [index] { @data[index] }
  [index]=(value) { @data[index] = value }

  size { @data.size }

  + { this }
  - {
    def size = @data.size
    def res = Vec.new(size, nil)
    for (i in 0...size) {
      res[i] = -@data[i]
    }
    return res
  }

  +(other) { .__operator(other, Fn.new {|a, b| a + b }) }
  -(other) { .__operator(other, Fn.new {|a, b| a - b }) }
  *(other) { .__operator(other, Fn.new {|a, b| a * b }) }
  /(other) { .__operator(other, Fn.new {|a, b| a / b }) }

  ==(other) {
    for (i in 0...@data.size) {
      if (@data[i] != other[i]) {
        return false
      }
    }
    return true
  }
  !=(other) { !(this == other) }

  __operator(other, binop) {
    def size = @data.size
    assert(other.size == size, "Other must have the same sime")
    def res = Vec.new(size, nil)
    if (other is Float) {
      for (i in 0...size) {
        res[i] = binop(@data[i], other)
      }
    } else {
      for (i in 0...size) {
        res[i] = binop(@data[i], other[i])
      }
    }
    return res
  }
}

def vec2(x, y) { Vec2.new(x, y) }
def vec3(x, y, z) { Vec3.new(x, y, z) }

def vec(size, value) {
  if (size == 2) {
    return Vec2.new(value)
  }
  if (size == 3) {
    return Vec3.new(value)
  }
  return Vec.new(size, value)
}
